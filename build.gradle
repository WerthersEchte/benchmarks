apply plugin: 'java'
apply plugin: 'idea'

repositories {
    jcenter()
    mavenCentral()
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'me.champeau.gradle:jmh-gradle-plugin:+'
    }
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'
    testCompile 'junit:junit:4.12'
}

// Stopwatch
sourceSets {
    stopwatch
}
dependencies {
    stopwatchCompile sourceSets.main.output
}

// jmh https://github.com/melix/jmh-gradle-plugin
apply plugin: 'me.champeau.gradle.jmh'

// https://github.com/melix/jmh-gradle-plugin
jmh {
    // https://github.com/CodeFX-org/lab-java8streamperformancebenchmark
    include = 'org\\.codefx\\.lab\\.streamperformance\\.*'
    humanOutputFile = null
    resultsFile = project.file("${project.buildDir}/results.txt")
    // useful for output https://github.com/jzillmann/jmh-visualizer
    resultFormat = 'JSON'

    timeOnIteration = '3s'
    iterations = 5
    benchmarkMode = 'sample'
    fork = 2
    timeUnit = 'ns'
    warmup = '3s'
    warmupBatchSize = 5
}

// caliper
sourceSets {
    caliper
}
dependencies{
    caliperCompile 'com.google.guava:guava:11.+'
    caliperCompile 'com.google.caliper:caliper:1.+'
    caliperCompile sourceSets.main.output
}
